#!/usr/bin/perl
#
# kindle_collect - read and write Amazon Kindle collections
#
# Copyright (C) 2011,2013  Christian Garbs <mitch@cgarbs.de>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

use Digest::SHA qw(sha1_hex);
use File::Find;
use JSON::Any;



### subroutines #################################################################

sub check_dir($)
{
    my $dir = shift;
    die "`$dir' does not exist\n"  unless -e $dir;
    die "`$dir' is no directory\n" unless -d $dir;
    
}

sub help()
{
    print <<"EOF";

kindle_collect [ --read <DIR> | --write <DIR> ]

Read and write collection data from an Amazon Kindle.

<DIR>    The directory your Kindle is mounted to.
--read   Prints the current collection in an easy to edit format to
         stdout.
--write  Read this format from stdin and copies it to your Kindle,
         overwriting your existing collections.  You might want to
         backup system/collections.json beforehand.

This is mostly untested and might (but should and propably will not)
break your Kindle.

EOF
    ;
}

sub find_docs($)
{
    my $dir = shift;
    my %docs;
    find(sub
	 {
	     if ( -f $File::Find::name ) {
		 $File::Find::name =~ s,^$dir/,,;
		 $docs{ '*'.sha1_hex( "/mnt/us/documents/$File::Find::name") } = $File::Find::name;
	     };
	 }
	 , $dir);
    return %docs;
}

sub find_audibles($)
{
    my $dir = shift;
    my %audibles;
    find(sub
	 {
	     if ( -f $File::Find::name ) {
		 $File::Find::name =~ s,^$dir/,,;
		 $audibles{ '*'.sha1_hex( "/mnt/us/audible/$File::Find::name") } = $File::Find::name;
	     };
	 }
	 , $dir);
    return %audibles;
}



### MAIN ########################################################################

# check parameters

if ( @ARGV != 2 or ($ARGV[0] ne '--read' && $ARGV[0] ne '--write') ) {
    help();
    exit;
}

# check directories

my $rootdir = $ARGV[1];
check_dir $rootdir;

my $docdir = $rootdir.'/documents';
check_dir $docdir;

my $audiodir = $rootdir.'/audible';
check_dir $audiodir;

my $sysdir = $rootdir.'/system';
check_dir $sysdir;

my $colfile = $sysdir.'/collections.json';


### READ ########################################################################

if ($ARGV[0] eq '--read') { ######## READ

    # parse collections file

    my $json = '{}';
    if ( -e $colfile ) {
	local $\ = undef;
	open COL, '<', $colfile or die "can't open `$colfile': $!\n";
	$json = <COL>;
	close COL or die "can't close `$colfile': $!\n";
    }

    my $j = JSON::Any->new;
    my $cols = $j->decode($json);

    # find all files
    
    my %files = ( find_docs $docdir, find_audibles $audiodir );

    # print collections
    
    my %hashes;
    foreach my $colname ( sort keys %{$cols} ) {

	my $col = $cols->{$colname};
	$colname =~ s/\@en-US$//;

	print "%% $colname\n";
	if ($col->{lastAccess}) {
	    print "++ $col->{lastAccess} (" . localtime($col->{lastAccess}/1000) . ")\n";
	} else {
	    print "++ " . time . "000 (no last access time)\n";
	}
	

	my @result;
	my $missing = 0;

	foreach my $item ( @{$col->{items}} ) {
	    if (exists $files{$item}) {
		push @result, $files{$item};
		push @{$hashes{$item}}, $colname;
	    } else {
		$missing++;
	    }
	}

	print "!\t$missing missing files\n" if $missing;
	print "\t$_\n" foreach (sort @result);
	print "\n";

    }

    # print uncollected

    print "%% ___unsorted___\n";

    my @result;

    foreach my $hash (keys %files) {

	if (! exists $hashes{$hash}) {
	    push @result, $files{$hash};
	}

    }

    print "\t$_\n" foreach (sort @result);
    print "\n";
    
}



### WRITE #######################################################################

elsif ($ARGV[0] eq '--write') {

    # find all files
    
    my %files = ( find_docs $docdir, find_audibles $audiodir );
    my %files_r;
    foreach (keys %files) {
	$files_r{$files{$_}} = $_;
    }

    # parse stdin
    
    my %cols;
    my $colname = '';
    
    while (my $line = <STDIN>) {

	chomp $line;
	$line =~ s/(\s+$|^\s+)//g;
	next if $line eq '';
	next if $line =~ /^!.*missing files/;
		    
	if ($line =~ /^%%\s*(.*)$/) {
	    $colname = $1;
	    $cols{$colname}->{NAME} = $colname;

	} elsif ($line =~ /^\+\+\s*(\d+)/) {
	    $cols{$colname}->{LASTACCESS} = $1;

	} else {
	    if (exists $files_r{$line}) {
		push @{$cols{$colname}->{ITEMS}}, $files_r{$line};
	    } else {
		die "line $.:unkown file `$line' in collection `$colname'\n";
	    }
	}

    }

    die "input file messed up, collection name must precede all other lines\n" if exists $cols{''};

    # write JSON

    my $json;
    foreach my $col (values %cols) {
	next if $col->{NAME} eq '___unsorted___';
	$json->{"$col->{NAME}\@en-US"} = {
	    'items' => exists $col->{ITEMS} ? $col->{ITEMS} : [],
	    'lastAccess' => exists $col->{LASTACCESS} ? ($col->{LASTACCESS} + 0) : ((time().'000') + 0),
	};
    }

    my $j = JSON::Any->new;
    open COL, '>', $colfile or die "can't open `$colfile': $!\n";
    print COL $j->encode($json);
    close COL or die "can't close `$colfile': $!\n";
 
}
